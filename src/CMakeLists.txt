# projects globals names
SET(STREAMER_NAME streamer CACHE STRING "Stream process name")
SET(STREAMER_COMMON ${STREAMER_NAME}_common)

SET(SOURCE_ROOT ${CMAKE_SOURCE_DIR}/src)

# for sources only absolute paths
SET(GLOBAL_COMMON_HEADERS
  ${CMAKE_SOURCE_DIR}/src/gst_constants.h
  ${CMAKE_SOURCE_DIR}/src/output_uri.h
  ${CMAKE_SOURCE_DIR}/src/inputs_outputs.h
  ${CMAKE_SOURCE_DIR}/src/input_uri.h
  ${CMAKE_SOURCE_DIR}/src/stream_struct.h
  ${CMAKE_SOURCE_DIR}/src/stream_stats.h
  ${CMAKE_SOURCE_DIR}/src/stream_commands.h
  ${CMAKE_SOURCE_DIR}/src/types.h
)
SET(GLOBAL_COMMON_SOURCES
  ${CMAKE_SOURCE_DIR}/src/gst_constants.cpp
  ${CMAKE_SOURCE_DIR}/src/output_uri.cpp
  ${CMAKE_SOURCE_DIR}/src/inputs_outputs.cpp
  ${CMAKE_SOURCE_DIR}/src/input_uri.cpp
  ${CMAKE_SOURCE_DIR}/src/stream_struct.cpp
  ${CMAKE_SOURCE_DIR}/src/stream_stats.cpp
  ${CMAKE_SOURCE_DIR}/src/stream_commands.cpp
  ${CMAKE_SOURCE_DIR}/src/types.cpp
)

SET(PROTOCOL_HEADERS
  ${CMAKE_SOURCE_DIR}/src/protocol/protocol.h
  ${CMAKE_SOURCE_DIR}/src/protocol/types.h
)
SET(PROTOCOL_SOURCES
  ${CMAKE_SOURCE_DIR}/src/protocol/protocol.cpp
  ${CMAKE_SOURCE_DIR}/src/protocol/types.cpp
)

SET(STREAM_COMMANDS_INFO_HEADERS
  ${CMAKE_SOURCE_DIR}/src/stream_commands_info/stop_info.h
  ${CMAKE_SOURCE_DIR}/src/stream_commands_info/restart_info.h
  ${CMAKE_SOURCE_DIR}/src/stream_commands_info/changed_sources_info.h
  ${CMAKE_SOURCE_DIR}/src/stream_commands_info/stream_stats_info.h
  ${CMAKE_SOURCE_DIR}/src/stream_commands_info/stream_struct_info.h
)
SET(STREAM_COMMANDS_INFO_SOURCES
  ${CMAKE_SOURCE_DIR}/src/stream_commands_info/stop_info.cpp
  ${CMAKE_SOURCE_DIR}/src/stream_commands_info/restart_info.cpp
  ${CMAKE_SOURCE_DIR}/src/stream_commands_info/changed_sources_info.cpp
  ${CMAKE_SOURCE_DIR}/src/stream_commands_info/stream_stats_info.cpp
  ${CMAKE_SOURCE_DIR}/src/stream_commands_info/stream_struct_info.cpp
)

SET(SDS_SOURCES
  ${CMAKE_SOURCE_DIR}/src/third-party/sds/sds.c
  ${CMAKE_SOURCE_DIR}/src/third-party/sds/sds_fasto.c
)

FIND_PACKAGE(Common REQUIRED)
FIND_PACKAGE(JSON-C REQUIRED)

IF(OS_WINDOWS)
  SET(PLATFORM_HEADER)
  SET(PLATFORM_SOURCES)
  SET(PLATFORM_LIBRARIES)
ELSEIF(OS_POSIX)
  SET(PLATFORM_HEADER)
  SET(PLATFORM_SOURCES)
  SET(PLATFORM_LIBRARIES pthread)
ENDIF(OS_WINDOWS)

ADD_SUBDIRECTORY(utils)

# common iptv lib

SET(STREAMER_COMMON_SOURCES
  ${STREAM_COMMANDS_INFO_HEADERS} ${STREAM_COMMANDS_INFO_SOURCES}
  ${PROTOCOL_HEADERS} ${PROTOCOL_SOURCES}
  ${GLOBAL_COMMON_HEADERS} ${GLOBAL_COMMON_SOURCES}
)
SET(STREAMER_COMMON_LIBRARIES ${STREAMER_COMMON_LIBRARIES} ${JSONC_LIBRARIES} utils ${COMMON_BASE_LIBRARY})
SET(PRIVATE_INCLUDE_DIRECTORIES_COMMON
  ${PRIVATE_INCLUDE_DIRECTORIES_COMMON}
  ${CMAKE_SOURCE_DIR}/src
)

ADD_LIBRARY(${STREAMER_COMMON} STATIC ${STREAMER_COMMON_SOURCES})
TARGET_INCLUDE_DIRECTORIES(${STREAMER_COMMON} PRIVATE ${PRIVATE_INCLUDE_DIRECTORIES_COMMON})
TARGET_LINK_LIBRARIES(${STREAMER_COMMON} ${STREAMER_COMMON_LIBRARIES})

SET(STREAMER_CORE ${STREAMER_NAME}_core)

IF(BUILD_SERVER)
  ADD_SUBDIRECTORY(server)
ENDIF(BUILD_SERVER)
IF(BUILD_STREAM)
  ADD_SUBDIRECTORY(stream)
ENDIF(BUILD_STREAM)

INSTALL(FILES ${CMAKE_SOURCE_DIR}/LICENSE DESTINATION . COMPONENT LICENSE RENAME LICENSE OPTIONAL)
INSTALL(FILES ${CMAKE_SOURCE_DIR}/COPYRIGHT DESTINATION . COMPONENT LICENSE RENAME COPYRIGHT OPTIONAL)
INSTALL(FILES ${PROJECT_CHANGELOG_FILE} DESTINATION . COMPONENT LICENSE RENAME CHANGELOG OPTIONAL)

IF (DEVELOPER_CHECK_STYLE)
  SET(CHECK_SOURCES ${STREAMER_COMMON_SOURCES})
  REGISTER_CHECK_STYLE_TARGET(check_style_${STREAMER_COMMON} "${CHECK_SOURCES}")
  REGISTER_CHECK_INCLUDES_TARGET(${STREAMER_COMMON})
ENDIF(DEVELOPER_CHECK_STYLE)

IF(DEVELOPER_ENABLE_TESTS)
  FIND_PACKAGE(GTest REQUIRED)

  ## Unit tests
  SET(PRIVATE_INCLUDE_DIRECTORIES_UNIT_TESTS
    ${PRIVATE_INCLUDE_DIRECTORIES_UNIT_TESTS}
    ${CMAKE_SOURCE_DIR}/src
  )
  SET(UNIT_TESTS_LIBS
      ${GTEST_BOTH_LIBRARIES}
      ${STREAMER_COMMON}
      ${PLATFORM_LIBRARIES})
  ADD_EXECUTABLE(unit_tests
    ${CMAKE_SOURCE_DIR}/tests/unit_test_output_uri.cpp
    ${CMAKE_SOURCE_DIR}/tests/unit_test_input_uri.cpp
    ${CMAKE_SOURCE_DIR}/tests/unit_test_types.cpp
  )
  TARGET_INCLUDE_DIRECTORIES(unit_tests PRIVATE ${PRIVATE_INCLUDE_DIRECTORIES_UNIT_TESTS} ${JSONC_INCLUDE_DIRS})
  TARGET_LINK_LIBRARIES(unit_tests ${UNIT_TESTS_LIBS})
  ADD_TEST_TARGET(unit_tests)
  SET_PROPERTY(TARGET unit_tests PROPERTY FOLDER "Unit tests")
ENDIF(DEVELOPER_ENABLE_TESTS)
